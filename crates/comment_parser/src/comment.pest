WHITESPACE     = _{ " " | "\t" }
NEWLINE        = _{ "\r\n" | "\n" }
ANY_WHITESPACE = _{ WHITESPACE | NEWLINE }

comment_content = {
    SOI ~ (type_block | other_text)* ~ EOI
}

// If we see "type:", then it's either a triple-tick enclosure or un-fenced content.
type_block = {
    "type:" ~ (WHITESPACE | NEWLINE)* // optional spacing/newlines
  ~ ("```" ~ block_content ~ "```" // triple-tick-fenced
  | block_content // free-text (unfenced)
  )
}

// Everything else that’s not a `type_block`.
other_text = _{ (!"type:" ~ ANY)+ }

// -------------------------------------------------------------------------
// The unified content rule for types
// -------------------------------------------------------------------------
block_content = {
    (ANY_WHITESPACE)* ~ (type_line)+ ~ (ANY_WHITESPACE)*
}

// A “type line” is `identifier :: type_expr`, e.g. `mapMe :: [a] -> (a -> b) -> [b]`.
type_line = {
    (ANY_WHITESPACE)* ~ identifier ~ "::" ~ type_expr ~ (NEWLINE | EOI)
}

// Identifiers: allow alphanumeric plus underscores
identifier = @{
    ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")*
}

// One segment in an arrow chain can be parenthesized, a list, or a simple/base type variable.
// A minimal arrow-based type expression, e.g. `Int -> String`, `[a] -> (a -> b) -> [b]`.
type_expr = {
    arrow_segment ~ ("->" ~ arrow_segment)*
}

// One segment in an arrow chain can be parenthesized, a list, or a simple/base type variable.
arrow_segment = { paren_type | list_type | type_ref }

paren_type = {
    "(" ~ type_expr ~ ")"
}

list_type = {
    "[" ~ type_expr ~ "]"
}

// a type var or reference to another type
type_ref = {
    primitive_ref
  | user_type
  | generic_ident
}

// generic params are lowercased
generic_ident = @{
    ASCII_ALPHA_LOWER ~ (ASCII_ALPHA_LOWER | ASCII_DIGIT | "_")*
}

// a reference to some user type alias must be uppercase
user_type = @{
    ASCII_ALPHA_UPPER ~ (ASCII_ALPHANUMERIC | "_")*
}

primitive_ref = {
    string_ref
  | int_ref
  | bool_ref
  | float_ref
  | path_ref
  | null_ref
}

// TODO: not sure if these should be case insensitive
// typescript does these as lowercase
string_ref = @{ "string" }
int_ref    = @{ "int" }
bool_ref   = @{ "bool" }
float_ref  = @{ "float" }
path_ref   = @{ "path" }
null_ref   = @{ "null" }
